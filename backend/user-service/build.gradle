plugins {
	id 'org.springframework.boot'
	id 'io.spring.dependency-management'
	id 'java'
}

group = 'com.mockio'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'org.yaml:snakeyaml:2.3'

	//swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

	implementation project(':common-core')
	implementation project(':common-spring')
	implementation project(':common-jpa')
	implementation project(':common-security')

	//p6spy
	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.0'

	//querydsl
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	//redis
//	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	//security
	implementation 'org.springframework.boot:spring-boot-starter-security'
	testImplementation 'org.springframework.security:spring-security-test'

	//jwt
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

	testImplementation platform("org.testcontainers:testcontainers-bom:1.20.3")

	//flyway
	implementation 'org.flywaydb:flyway-core'
	runtimeOnly "org.flywaydb:flyway-database-postgresql"
	runtimeOnly "org.postgresql:postgresql"
	testImplementation "org.testcontainers:junit-jupiter"
	testImplementation "org.testcontainers:postgresql"
	testImplementation "org.springframework.boot:spring-boot-testcontainers"

	//kafka
	implementation "org.springframework.kafka:spring-kafka"
	testImplementation "org.springframework.kafka:spring-kafka-test"

	//actuator
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation "io.micrometer:micrometer-registry-prometheus" //프로메테우스

	implementation "io.hypersistence:hypersistence-utils-hibernate-63:3.7.2"

}

tasks.named('test') {
	useJUnitPlatform()
	systemProperty "spring.profiles.active", "test"  //test는 profile test로
}
//tasks.withType(Test).configureEach {
//	enabled = false
//}

// ==============================
// QueryDSL Q-class generation
// ==============================
def querydslGeneratedDir = file("$buildDir/generated/sources/annotationProcessor/java/main")

tasks.withType(JavaCompile).configureEach {
	// Q파일 생성 경로를 build/ 아래로 강제
	options.generatedSourceOutputDirectory.set(querydslGeneratedDir)
}

sourceSets {
	main {
		java {
			srcDirs += querydslGeneratedDir
		}
	}
}

tasks.named("clean") {
	doFirst {
		delete querydslGeneratedDir
	}
}